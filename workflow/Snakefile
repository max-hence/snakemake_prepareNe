from os import path

configfile: "config/config.yml"

# Classic filter
include: "rules/common.smk"
include: "rules/prepare_Neinputs.smk"
include: "rules/prepare_vcf.smk"
include: "rules/prepare_msmc2.smk"

# Force chr name matching in paths
wildcard_constraints:
    chr = "|".join(get_chr_list(config["fai_path"])),
    prefix = config["final_prefix"],
    sample = "|".join(get_sample_list(config["pop_path"]))

if config["callability"]:
    module callability:
        snakefile:
            "modules/callability/Snakefile"
        config:
            config

    use rule * from callability as callability_*

if config["density"]:
    module density:
        snakefile:
            "modules/gene_density/Snakefile"
        config:
            config
    use rule * from density as density_*

if config["recombination"]:
    module recombination:
        snakefile:
            "modules/recombination/Snakefile"
        config:
            config
    use rule * from recombination as recombination_*

rule all:
    input:
        get_output()
    default_target: True